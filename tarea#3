	// Funciones de usuario para el Algoritmo Menu
	// Presenta el menu en la pantalla
	Funcion opc = presentarMenu(titulo,opciones,lim) 
		Escribir titulo
		Escribir ""
		Para pos=1 Hasta lim  Hacer
			Escribir opciones[pos]
		Fin Para
		Escribir "Elija opcion[1...",lim,"]: "
		Leer opc
FinFuncion
// mostras si 2 cadenas son iguales o diferentes 
Funcion cadenas_igales_o_diferentes(x,c)
	Dimension cadena1[3], cadena2[3]
	Para x= 1 Hasta 3 Con Paso 1 Hacer
		escribir "ingrese un numero en la cadena 1" Sin Saltar
		leer cadena1(x)
		escribir "ingrese un numero en la cadena 2" Sin Saltar
		leer cadena2(X)
	Fin Para
	c = 0
	Para x=1 Hasta 3 Con Paso 1 Hacer
		si cadena1(x) == cadena2(X) Entonces
			c = c +1
		FinSi
	Fin Para
	si c == 3 Entonces
		escribir "las cadenas son iguales"
	SiNo
		escribir "las cadenas son diferentes"
	FinSi
FinFuncion
//notas promedios en un arreglo
Funcion notas_promedios_arreglos( n, acum, prom)
	Dimension arreglo[n]
	Para i<-1 Hasta n Con Paso 1 Hacer
		escribir " ingrese un valor para almacenar"
		leer v 
		arreglo[i] <- v
	Fin Para
	Para j <- 1 Hasta n Con Paso 1 Hacer
		acum <- acum + arreglo[j]
	Fin Para
	prom <- acum/n
	escribir "el promedio del arreglo es:" , prom
FinFuncion
//numeros primos en un arreglo
Funcion numeros_primos_arreglo(num, contador)
	Dimension vector[25]
	b = 1
	Para num = 1 Hasta 100 Con Paso 1 Hacer
		a= 1
		contador = 0
		Mientras a <= num Hacer
			si num mod a == 0 Entonces
				contador = contador + 1
			FinSi
			a = a +1
		Fin Mientras
		si contador ==2 Entonces
			vector(b) = num
			b = b + 1
		FinSi
	Fin Para
	escribir " estos son los numeros primos"
	Para num = 1 Hasta 25 Con Paso 1 Hacer
		escribir vector(num)
	Fin Para
FinFuncion
//Funcion posicion de un arreglo
Funcion posicion_de_arreglo
	Dimension  arreglo[10]
	arreglo[1] = "esta es la posicion 1"
	arreglo[2] = "esta es la posicion 2"
	arreglo[3] = "esta es la posicion 3"
	arreglo[4] = "esta es la posicion 4"
	arreglo[5] = "esta es la posicion 5"
	arreglo[6] = "esta es la posicion 6"
	arreglo[7] = "esta es la posicion 7"
	arreglo[8] = "esta es la posicion 8"
	arreglo[9] = "esta es la posicion 9"
	arreglo[10] = "esta es la posicion 10"
	Repetir
		escribir "porfavor ingresa un valor entre 1 y 10 para consultar en el arreglo"
		leer p
	Hasta Que p > 0 y p < 11
	Para i <- 1 Hasta p Con Paso 1 Hacer
		si i = p Entonces
			escribir "usted " , arreglo[p]
		FinSi
	Fin Para
FinFuncion
//pago de sueldos 
funcion PagoSueldo(sueldo,horastrabajadas,valorhora,horas50,horas100,descuento)
	sueldo = horastrabajadas * valorhora
	sobretiempo = (horas50 * 1.5 + horas100 * 2) * valorhora
	totalingreso = sueldo + sobretiempo
	descuento = totalingreso * 9.35 / 100
	liquido = totalingreso - descuento
	Escribir ""
	Escribir "El sueldo es: ", sueldo
	Escribir "El sueldo por sobretiempo es: ", sobretiempo
	Escribir "El ingreso total adquirido es: ", totalingreso
	Escribir "El descuento fue: ",  descuento
	escribir "El total líquido a recibir es: ", liquido
	escribir ""
	Escribir "gracias estimado usuario"
FinFuncion
//numero primos
Funcion numeros_primos1(indice,indice1)
	Dimension a[20]	
	Para indice <- 1 Hasta 20 Con Paso 1 Hacer
		Escribir "ingrese un numero ", indice
		Leer a[indice]
	FinPara
	Para indice <- 1 Hasta 20 Con Paso 1 Hacer
		indice1 <- 2
		Mientras indice1 < a[indice] & a[indice] % indice1 <> 0 HACER
			indice1 <- indice1 + 1
		FinMientras
		Si (indice1 = a[indice]) Entonces
			Escribir a[indice], " es primo"
		FinSi
	FinPara
FinFuncion
//funcion de arreglos invertidos 
Funcion arregloinvertido(n)
	Dimension vector1[n],vector2[n]
	para x = 1 Hasta n Con Paso 1 Hacer
		Escribir 'Ingresa un número'
		leer vector1(x)
	FinPara
	vector2(1) = vector1(n)
	para x = 1 Hasta n-1 Con Paso 1 Hacer
		vector2(x+1) = vector1(x)
	FinPara
	Escribir 'los numero ingresados al arreglo invertidos son'
	para x = 1 Hasta n Con Paso 1 Hacer
		Escribir vector2(x)
	FinPara
FinFuncion
// funciones de numero primos
Funcion rellenararreglo(arreglo por referencia,dim)
	Definir i Como Entero
	Para i = 1 Hasta dim Hacer
		arreglo[i]=Aleatorio(2,800)
	FinPara
FinFuncion
Funcion mostrarprimos(arreglo por referencia, dim)
	definir i,primos Como Entero
	primos=0
	Para i =1 Hasta dim Hacer
		si esprimo(arreglo[i]) Entonces
			primos = primos +1
			Escribir arreglo[i] Sin Saltar
			Escribir " " Sin Saltar
		FinSi
	FinPara
	Escribir "de ", dim , " numeros hay ", primos, " primos"
FinFuncion
Funcion ok=esprimo(n)
	Definir ok Como Logico
	Definir i como entero
	ok=Verdadero
	para i=2 Hasta (n-1) Hacer
		si n%i == 0 Entonces
			ok=Falso
			i = (n-1)
		FinSi
	FinPara
FinFuncion
//arreglos 
Funcion arreglos(cantidad,i)
	cantidad<-5; 
	Dimension num[cantidad]
	Para i<-1 Hasta cantidad Con Paso 1 Hacer
		Escribir "Ingrese un numero para la posición ",i 
		Leer num[i]
	FinPara
	Escribir "Los numeros ingresados son : "
	Para i<-1 Hasta cantidad Con Paso 1 Hacer
		Escribir num[i]
	FinPara
FinFuncion
//suma de arreglos
Funcion suma_de_arreglos(i,suma,arreglo1,arreglo2 )
	dimension a[1] 
	dimension b[1]
	dimension c[1]
	para i <- 1 hasta 1 con paso 1 hacer
		escribir "introduce un número para el vector arreglo1"
		leer a[i]
		escribir "introduce un número para el vector arreglo2"
		leer b[i]
	Finpara
	Para i <- 1 hasta 1 con paso 1 hacer
		suma <- a[i] + b[i]
		c[i] <- suma
	Finpara
	Para i <- 1 hasta 1 con paso 1 hacer
		escribir " las sumas de arreglo1 + arreglo2 son: ", c[i]
	Finpara
FinFuncion
// el mayor de un arreglo de 10 digitos
Funcion mayor_de_un_arreglo(n,may,men)
	Dimension num[10]  
	para i = 1 Hasta 10 Con Paso 1 Hacer
		Escribir "Ingrese el numero " , i
		Leer n 
		num[i] = n
	FinPara
	para i = 1 Hasta 10 Con Paso 1 Hacer
		si i == 1 Entonces
			may = num[i]
			men = num [i]
		SiNo
			si num[i] > may Entonces
				may = num [i]
			SiNo
				si num[i] < men Entonces
					men = num[i]
				FinSi
			FinSi
		FinSi
	FinPara
	Escribir " el numero menor es: ", men
	Escribir " el numero mayor es: ", may
FinFuncion
// cuantas palabras y cuantas letras 
Funcion palabras(posicion ,cont,cad)
	cont <- 0 
	posicion <- 0 
	Mientras  Subcadena( cad , posicion , posicion) = " " Hacer
		posicion <- posicion + 1 
	FinMientras
	Para  posicion <-posicion hasta Longitud(cad)-1 Hacer
		Si  Subcadena(cad,posicion,posicion)=" " Entonces
			cont <- cont + 1 
			Mientras  Subcadena(cad,posicion,posicion)=" " Y posicion <= Longitud(cad) Hacer
				posicion <- posicion + 1 
			FinMientras
		finsi
	finpara
	Si  subcadena(cad,Longitud(cad)-1,Longitud(cad)-1)<>" " Entonces
		cont <- cont + 1 
	finsi
	Escribir  "La frase tiene " , cont , " palabras" 
	Escribir " la frase tiene " , posicion , " de letras contando los espacios" 
FinFuncion
// palabra polindromo
Funcion polindromo(a,b,c,x,palabra)
	b = Longitud(palabra)
	a = 1
	x = 0
	Mientras a < b Hacer
		si Subcadena( palabra,a,a) <> Subcadena(palabra,b,b) Entonces
			x = x +1
		FinSi
		a = a +1
		b = b -1
	FinMientras
	si x == 0 Entonces
		Escribir " la palabra ", palabra, " es polindromo" 
	SiNo
		Escribir " la palabra  ", palabra, " no es polindromo"
	FinSi
FinFuncion
//numeros amigos 
Funcion num_amigos(num1,num2,sumas)
	num1=0;num2=0;sumas=0;x=1
	Mientras x<num1 Hacer
		si num1 mod x=0 Entonces
			sumas=sumas+x
		FinSi
		x=x+1
	FinMientras
	si sumas=num2 Entonces
		Escribir "los numeros son amigos"
	SiNo
		Escribir "los numeros no son amigos"
	FinSi
FinFuncion
//numero perfecto
Funcion numero_perfecto(num,num1,perfecto)
	num=0;num1=2;perfecto=0
	Escribir "por favor ingrese el numero"
	leer num
	Mientras num1<=num Hacer
		si num mod num1=0 Entonces
			Escribir num1 
			perfecto= perfecto+(num/num1)
		FinSi
		num1=num1+1
	FinMientras
	si perfecto=num Entonces
		Escribir "el numero ",num," es perfecto"
	sino 
		Escribir "el numero ",num," no es perfecto"
	FinSi
FinFuncion
//divisores de un numero
Funcion divisores_de_un_numero(num1)
	para i=1 Hasta num1 Hacer
		si num1 mod i=0 Entonces
			Escribir i
		FinSi
	FinPara
FinFuncion
//Serie de fibonacci hasta N 
Funcion  fibonacci(num,num1,num2,num3)
	num2=1
	Para i=1 Hasta num Hacer
		Escribir num1
		num3=num1+num2
		num1=num2
		num2=num3
	FinPara
FinFuncion
// convertir de base 2 a base 10
Funcion binario_a_decimal(x,num,decimal,binario)
	binario = ConvertirATexto(num)
	num = Longitud(binario)
	x = 0
	decimal = 0
	Mientras num >= 0 Hacer
		si Subcadena(binario,num,num) = "1" Entonces
			decimal= decimal+2^x 
		FinSi
		num = num - 1
		x = x + 1
	FinMientras
	Escribir "El numero en decimal es: ",decimal
FinFuncion
// cuantos dijitos de un numero 
Funcion digitos_de_numero(dig,num)
	Mientras num>=1 Hacer
		num=trunc(num/10)
		dig= dig + 1
	Fin Mientras
	Escribir "el numero ingresado tiene ",dig, " digitos"
FinFuncion
// se muestra una secuencia de numeros y se muestra su factorial, el porceso termina al colocar el o
Funcion factorial_hasta_0(num1,num2,fac)
	num1=1;num2=1;fac=1
	Mientras num1<>0 Hacer
		escribir "ingresa un numero entero"
		leer num1
		num2=1;fac=1
		mientras num2 <= num1 Hacer
			fac=fac*num2
			num2=num2+1
		FinMientras
		Escribir "el factorial de ", num1, " es: ", fac
	FinMientras
FinFuncion

//base elevada al exponente
Funcion baseelevada(num1,num2)
	r <- num1^num2
	Escribir 'el resultado es ',r
FinFuncion
//validar un numero positivo
Funcion validar_positivo(n1)
	n1=0
	mientras n1<=0 Hacer
		Escribir "ingrese un numero "
		leer n1 
	FinMientras
	Escribir n1,"  es numero positivo por lo tanto finaliza la ejecucion"
FinFuncion
//  presentar el promedio de los numeros que sean >= 100 and <=500 
Funcion promedio_de_100_500(a,num,num1,suma)
	a=1
	Mientras a<=num Hacer
		Escribir 'ingrese el numero a promediar ', a
		Leer num1
		si num1>=100 Y num1<=500
			suma <- suma+num1
			a <- a+1
		FinSi
	FinMientras
	Escribir 'el promedio es: ',suma/num
FinFuncion
// suma y promedio de numeros negativos 
funcion sumaypromedio_de_negativos(num1,num2,sum,prom)
	para i=1 hasta num1 Hacer
		Escribir "ingrese numeros "
		leer num2 
		si num2 <0 Entonces
			c=c+1
			sum=sum+num2
		FinSi
	FinPara
	prom=sum/c
	Escribir "la cantidad de numeros negativos es : ", c
	Escribir "la suma total es: ", sum
	Escribir "el promedio es: ", prom
FinFuncion
// pares de 2 hasta n 
Funcion pares2_n(num1,i)
	para i=2 hasta num1 Hacer
		si i mod 2=0 Entonces
			Escribir i
		FinSi
	FinPara
FinFuncion
// positivos de una secuencia 
Funcion  positivos(cantidad,num,i,positiv)
	para i=1 hasta cantidad Hacer
		Escribir "ingrese numeros   ", i
		leer num
		si num >= 1 Entonces
			positiv=positiv+1
		FinSi
	FinPara
	Escribir "hay ",positiv," positivos"
FinFuncion
//mayor y menor de una secuencia 
Funcion mayor_menor(i, cantidad,may,men,num)
	i=1
	mientras i <= cantidad Hacer
		Escribir "ingrese numero ", i
		Leer num
		si i == 1 Entonces
			may=num
			men=num
		SiNo
			si num > may Entonces
				may = num
			FinSi
			si num < men Entonces
				men = num
				
			FinSi
		FinSi
		i = i+1
	FinMientras
	Escribir "el mayor es : ", may
	Escribir " el menor es : ", men 
FinFuncion
// suma de los multiplos de 5
Funcion  secuencia_sumaymultiplo_5(num1,sum,pro)
	num1 <- 0; pro <- 1; sum <- 0
	Mientras num1<> -1 Hacer 
		Escribir 'ingrese numeros(-1 para finalizar)'
		Leer num1
		Si (num1 MOD 5=0) Entonces
			sum <- sum+num1
		FinSi
	FinMientras
	Escribir 'la suma total es: ',sum
FinFuncion
//multiplos
Funcion multiplos(num1,num2)
	si num1 mod num2 =0 Entonces
		Escribir "son multiplos"
	sino 
		Escribir "no son multiplos"
	FinSi
FinFuncion
//nota alumnos
Funcion notaalumnos(nota1,nota2,total)
	total = (nota1+nota2)/2
	si total >= 70 Entonces
		Escribir "esta aprobado con ", total 
	sino 
		Escribir "esta reprovado con ", total 
	FinSi
FinFuncion
//positivo o negativo 
Funcion positivonegativo(num)
	si num > 0 Entonces
		Escribir " el numero: ",num," es positivo" 
	SiNo
		Escribir "el numero: ",num, " es negativo" 
		
	FinSi
FinFuncion
//par o impar 
Funcion parimpar(num1)
	si num1%2=0 Entonces
		Escribir num1, " el número es par" 
	sino 
		Escribir num1, " el número no es par" 
	FinSi
FinFuncion

// precio de la compra 
Funcion compra(preciocompra, descuento, totaldescuento,IVA)
	si preciocompra >= 100 Entonces
		IVA = preciocompra *0.12
		descuento = preciocompra*0.1
		des2=preciocompra*0.05
		totaldescuento=preciocompra-descuento 
		totaldescuento=totaldescuento-des2 + IVA
		Escribir "precio total de compra: ", preciocompra
		Escribir " el iva del 12%       : ", IVA 
		Escribir "                  -10%: ", descuento
		Escribir "  precio total a pagar: ", totaldescuento
	sino 
		IVA = preciocompra *0.12
		des2 = preciocompra*0.05
		totaldescuento = preciocompra-des2 + IVA
		Escribir "precio total de compra: ", preciocompra
		Escribir " el iva del 12%       : ", IVA 
		Escribir "                   -5%: ", des2
		Escribir "  precio total a pagar: ", totaldescuento
	FinSi
	Escribir  ""
	Escribir "gracias por su compra "
FinFuncion

// si el primer numero es mayor se suma si es menor se resta 
Funcion sumaoresta(num1,num2)
	si num1 >= num2 Entonces
		Escribir num1 " + " num2 " = ", num1 + num2
	SiNo
		Escribir num1 " - " num2 " = ", num1 - num2 
	FinSi
FinFuncion 
// Calcula el proceso matematico de dos numeros dada su operacion
Funcion calculadora(operacion,num1,num2)
	Si operacion = "+" Entonces
		Escribir num1,"+",num2,"=",num1+num2
	SiNo
		Si operacion = "-" Entonces
			Escribir num1,"-",num2,"=",num1-num2
		SiNo
			Si operacion = "*" Entonces
				Escribir num1,"*",num2,"=",num1*num2
			SiNo
				Si operacion = "/" Entonces
					Escribir num1,"/",num2,"=",num1/num2
				SiNo
					Si operacion = "%" Entonces
						Escribir num1,"%",num2,"=",num1%num2
					SiNo
						Si operacion = "^" Entonces
							Escribir num1,"^",num2,"=",num1^num2
						SiNo
							Escribir "Operacion Incorrecta, intentelo de nuevo"
						FinSi
					FinSi
				FinSi
			FinSi
		FinSi
	Fin Si
FinFuncion
// Verifica si un numero es primo o no y devueve 1 si es primo y 0 si no es
Funcion bandera=primo(num)
	Definir r,c,bandera Como Entero
	bandera=1;c=2
	// recorro mientras sea primo y c no lleaga al numero
	Mientras bandera=1 y c < num Hacer
		r=num MOD c
		Si r=0 Entonces
			bandera=0
		SiNo
			c=c+1
		Fin Si
	Fin Mientras
	
FinFuncion
// Recorrer y presentar cadena caracter por caracter
Funcion presentarCadena(texto)
	Definir pos,lon Como Entero
    //Escribir Subcadena(texto,2,3)
	lon = Longitud(texto)
	Para pos=1 Hasta lon  Hacer
		Escribir Subcadena(texto,pos,pos)
	Fin Para
FinFuncion
// recorre y cadena y cuenta las vocales de dicha cadena
Funcion vocales(texto)
	Definir pos,lon Como Entero
    //Escribir Subcadena(texto,2,3)
	lon = Longitud(texto)
	Para pos=1 Hasta lon  Hacer
		Si Subcadena(texto,pos,pos) = 'a' | Subcadena(texto,pos,pos)='e' | Subcadena(texto,pos,pos)='i' | Subcadena(texto,pos,pos)='o' | Subcadena(texto,pos,pos)='u' Entonces
			cv = cv +1
		Fin Si
		reves=Subcadena(texto,pos,pos)+reves
	Fin Para
    Escribir "Frase al revez: ",reves,"  No vocales: ",cv
FinFuncion
// Ingresar datos a un arreglo
Funcion ingresarArreglo(vector por referencia,lim)
	Definir num,pos Como Entero
	Para pos<-1 Hasta lim Hacer
		Escribir "Ingrese numero"
		leer num
		vector[pos]=num
		
	Fin Para
FinFuncion
// Presenta los datos de un arreglo dada una posicion inicial y final
Funcion presentarArreglo(vector,inicio,lim)
	Definir pos Como Entero
	Si lim > 0 Entonces
		Para pos<-inicio Hasta lim Hacer
			Escribir pos,"[",vector[pos],"]"
		Fin Para
	SiNo
		Escribir "Ingrese primero datos al arreglo"
	Fin Si
FinFuncion
//combertir a base 2 a base 16
Funcion resultado = conversordesdedecimal(num,base)
	Definir resultado,digito Como Caracter
	Definir cociente Como Entero
	resultado <- ''; digito <- ''; cociente <- 0
	Si num=0 Entonces
		resultado <- '0'
	FinSi
	Mientras num>0 Hacer
		cociente <- num MOD base
		digito <- ConvertirATexto(cociente)
		Si cociente>9 Entonces
			digito <- letrashex(cociente)
		FinSi
		resultado <- Concatenar(digito,resultado)
		num <- trunc(num/base)
	FinMientras
FinFuncion
Funcion resultado = letrashex(cociente)
	Definir resultado Como Caracter
	Segun cociente  Hacer
		10:
			resultado <- 'A'
		11:
			resultado <- 'B'
		12:
			resultado <- 'C'
		13:
			resultado <- 'D'
		14:
			resultado <- 'E'
		15:
			resultado <- 'F'
	FinSegun
FinFuncion
// buscac un valor en un arreglo y retorna la posicion si lo encuentra sino retorna -1
Funcion posicion=buscarArreglo(buscado,vector,lim)
	Definir pos,encontrado Como Entero
	pos=1;encontrado=0
	mientras encontrado=0 y pos <= lim Hacer
		Si vector[pos]=buscado Entonces
			encontrado=1
		SiNo
			pos = pos + 1
		Fin Si
	Fin Mientras
	Si encontrado=1 Entonces
		posicion=pos
	SiNo
		posicion = -1
	Fin Si
FinFuncion
// Algoritmo principal
Algoritmo Menu
	Definir pos,resp,limite,dato Como Entero
	Definir opcion,opc1,opc2,opc3,frase Como Caracter
	Dimension menuPrincipal[4],menuNumeros[21],menuRazonamiento[11],menuCadenaVectores[16]
    Dimension arreglo[100]	
	limite=0
	// ***** MENU PRINCIPAL ******
	menuPrincipal[1] = "1)Ejercicios Basicos"
	menuPrincipal[2] = "2)Ejercicios Razonamiento"
	menuPrincipal[3] = "3)Ejercicios Cadenas y Vectores"
	menuPrincipal[4] = "4)Salir"
	
	// ***** EJERCICIOS BASICOS ******
	// dado 2 numeros presentar la suma
	menuNumeros[1] = "1)Sumar dos numeros"
	// dado 2 numeros si el primero es  >= al segundo sumarlo sino restarlo
	menuNumeros[2] = "2)Sumar o restar"
	// ingrese 2 numeros con una operacion matematica("+,-,*,/,%,^") 
	// realizar y presentar la respuesta de la operacion matematica
	menuNumeros[3] = "3)Caculadora"
	// presentar el mayor de 2 numeros ingresados
	menuNumeros[4] = "4)Mayor de dos numeros"
	// presentar el menor de 3 numeros ingresados
	menuNumeros[5] = "5)Mayor de tres numeros"
	// La despensa "El Baraton", a todas las ventas que pasen de $100,
	//se les aplicará un  descuento del 10%, a  todo los demás se les aplicará sólo el 5% 
	// luego del recargo del iva del 12%
	menuNumeros[6] = "6)Comprar productos"
	// Ingresar el nombre,horas trabajadas, valor hora, horas50 y horas100 de sobretiempo
	// se pide calcular el rol del pago del trabajor dado los siguientes calculos.
	// sueldo del trabajador(horasTrajabadas por el valorHora)
	// el sobretiempo=(horas50*1.5+horas100*2)*Valor
	// total ingreos = sueldo+sobretiempo
	// descuento= totalingresos*9.35%
	// liquido= totalingresos - descuento
	menuNumeros[7] = "7)Pago de Sueldos"
	// dada dos notas calcular el promedio y presentar el mensaja "Aproboado" si el promedio
	// mayor 70 y reprobado si es menor que 70
	menuNumeros[8] = "8)Notas de Alumnos"
	// dado un numero indicar si es positivo o negativo
	menuNumeros[9] = "9)Positivo/Negativo"
	// dado un numero indicar si es par o impar
	menuNumeros[10] = "10)Par e Impar"
	// dado dos nmeros indicar si el numero1 es multiplo del numero2
	menuNumeros[11] = "11)Multiplo de cualquier Numero"
	// dada una secuencia de numeros presentar el mayor y el menor de esa esa secuencia
	menuNumeros[12] = "12)El Mayor y Menor de una secuencia de numeros"
	// dada una secuencia de numeros presentar cuantos son numeros positivos
	menuNumeros[13] = "13)Positivos de una secuencia de numeros"
	// dada una serie d enumeros presntar la suma de lo numeros multiplos de 5
	menuNumeros[14] = "14)Suma de los multiplos de cinco de una serie de numeros"
	// presentar los numeros pares desde 2 hasta N
	menuNumeros[15] = "15)Generar y presentar los Numeros pares del 2 a N"
	// dada una serie de numeros presente el numero siempre y cuando sea negativo
	// y antes de finalizar presente la cantidad, la suma y el promedio de dichos numeros negativos
	menuNumeros[16] = "16)Cantidad, Suma y Promedio de numeros negativos de una serie"
	// dado una serie de numeros presentar el promedio de los numeros que sean >= 100 and <=500 
	menuNumeros[17] = "17)Serie promedio de rango de numeros"
	// Ingrese un numero si este es postivo finalice sino vuelva a ingresarlo
	menuNumeros[18] = "18)Validar que un numero sea positivo"
	// dado dos numeros base y exponente. calcular la base elevada al exponente 
	menuNumeros[19] = "19)Base y exponente"
	// dada una serie de numeros calcular los factorles de dichos numero la serie termina
	// cuando un numero de la serie sea igual a cero.
	menuNumeros[20] = "20)Serie Factoriales"
	menuNumeros[21] = "21)Salir"
	
	// ******EJERCICIOS DE RAZONAMIENTO SIN USAR CADENAS Y VECTORES*******
	// contar los digitos de cualquier numero: ej: 342 = 3 digitos
	menuRazonamiento[1] = "1)Cuantos Digitos tiene un Numero"
	// ejemplo: binario= 1111101000  ==> 1000 (decimal) Respuesta = 3E8 (hexadecimal)
	menuRazonamiento[2] = "2)Covertir de Base 2 a Base 10"
	// ejemplo: binario= 1100100  ==> 100 (decimal) => 
	menuRazonamiento[3] = "3)Convertir de Base 2 a Base 16 pasando por Base 10"
	// ejemplo Si n=8 => La serie de fibonacci es = 0 1 1 2 3 5 8 13
	menuRazonamiento[4] = "4)Serie de fibonacci hasta N"
	// ejemplo si numero=10 ==> resp= 1,2,5
	menuRazonamiento[5] = "5)Divisores de un Numero"
	// cuando los divisores de un numero es igual al numero se dice que ese numero es perfecto
	// si numero es 6 los divisores son 1,2,3 sumados igual a 6 es igual al numero ==> es perfecto
	menuRazonamiento[6] = "6)Numero Perfecto"
	// un numero es primo cuando no tiene divisores excepto el 1 y el propio numero
	// ejemplo 5 no tiene divsores es "primo"  9 tiene como divisor al 3 por tanto no es "primo"
	menuRazonamiento[7] = "7)Numero Primo"
	// dos numeros son amigos si la suma de los divisores del primer numero es = a la suma de los
	// divisores del segundo numero Eje: numero=6 (1+2+3)=6 numero 25(1+5)=6 ==> 6=6 son amigos 
	menuRazonamiento[8] = "8)Verificar si dos numeros son Amigos"
	// dos numeros son primos gemelos si ambos numeros son primos en valor absoluto y la resta de
	// los 2 numeros es == 2 eje: 11 y 13 abs(11-13)==2
	menuRazonamiento[9] = "9)Verificar si dos Numeros son Primos Gemelos"
	// por cada numero de una serie verifique si es primo, si es primo contarlo
	menuRazonamiento[10] = "10)De una serie de numeros cuantos son Primos"
	menuRazonamiento[11] = "11)Salir"
	
	// ****** EJERCICIOS DE CADENAS Y VECTORES *******
	// dado n valores ingresarlos a un arreglo
	menuCadenaVectores[1] = "1)llenar un arreglo de numeros"
	// recorrer un arreglo y presentarlo uno por uno
	menuCadenaVectores[2] = "2)Presentar los elementos de un arreglo"
	// buscar el dato y presentar la posicion en que se encuntra el dato en el arreglo
	menuCadenaVectores[3] = "3)Buscar un dato en un arreglo"
	// Presentar el mayor de los datos de un arreglo
	menuCadenaVectores[4] = "4)Elemento Mayor de un arreglo"
	// Dado un arreglo copie los datos en otro de atras para delante 
	menuCadenaVectores[5] = "5)Copiar los datos de un arreglo en otro invertido"
	// Dado los arreglos1 y arreglos 2 sumarlos valor por valor del arreglo1 y arreglo2
	// y asigmarlo en el arreglo3(los tres arreglos deben tener la misma longitud)
	menuCadenaVectores[6] = "6)Dados los arreglos arreglo1[] y arreglo2[] Sumarlos en al arreglo Suma[]"
	// de una serie de numeros solo guardar en un arreglo los nymeros primos
	menuCadenaVectores[7] = "7)Dado N numeros guardar en un arreglo los numeros primos"
	// dadas las notas guardades en un arreglos obtener el promedio de las notas >= 70
	menuCadenaVectores[8] = "8)Dadas los datos de un arreglo obtener el promedio de las notas >=> 70"
	// presentar caracter por caracter de una cadena
	menuCadenaVectores[9] = "9)Dada una cadena presentarla caracter por caracter"
	// presentar  una cadena al reves
	menuCadenaVectores[10] = "10)Dada una cadena presentarla invertida"
	// contar las vocales de jque tiene una cadena cualquiera
	menuCadenaVectores[11] = "11)Dada una cadena indicar cuantos vocales tiene"
	// contar las palabras de una cadena
	menuCadenaVectores[12] = "12)Dada una cadena indicar cuantos palabras tiene"
	// buscar si un caracteer ingresado se encuentra en una cadena si es asi presentarMenu
	// la posicion en que se encuntra ese caracter en la cedena caso contrario presentar -1
	menuCadenaVectores[13] = "13)presentar la posicion de un caracter buscado dentro de una cadena"
	// comparar 2 cadenas csracter por catacter e indicar si son iguales o no
	menuCadenaVectores[14] = "14)Dadas dos cadenas indicar si son iguales caracter por caracter"
	// una cadena es palaindorma si se lee de la misma forma de izquierda a derecha 
	// ejemplo "ana"
	menuCadenaVectores[15] = "15)Indicar si una cadena es palindroma"
	menuCadenaVectores[16] = "16)Salir"
	// cuando se escoja la opcion 4 termina el programa
	opcion=''
	Mientras opcion <> "4" Hacer
		Borrar Pantalla
		opcion = presentarMenu("********** M E N U   P R I N C I P A L **********",menuPrincipal,4)
		Borrar Pantalla
		Segun opcion Hacer
			"1":
				opc1=""
				Mientras opc1<>"21" Hacer
					opc1= presentarMenu("********** M E N U  E J E R C I C I O S  B A S I C O S **********",menuNumeros,21)
					Borrar Pantalla
					Segun opc1 Hacer
						"1":
							Escribir "Sumar Dos  Numeros"
							Escribir "la operacion de la suma de dos numeros "
							Leer ope
							Escribir "dame el primer numero"
							Leer num1
							Escribir "dame el segundo numero"
							Leer num2
							calculadora(ope,num1,num2)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"2":
							Escribir "Sumar o restar" 
							Escribir "ingrese el primer numero "
							Leer num1
							Escribir "ingrese el segundo numero"
							Leer num2
							sumaoresta(num1,num2)
							Escribir "espere por favor, Regresando al Menu... "
							Esperar 3 Segundos
							
						"3":
							Escribir "Calculadora"
							Escribir "Ingrese Operacion[+,-,*,/,%,^]: " Sin Saltar
							Leer ope
							Escribir "Ingrese Numero 1: " Sin Saltar
							Leer num1
							Escribir "Ingrese Numero 2: " Sin Saltar
							Leer num2
							calculadora(ope,num1,num2)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"4":
							Escribir "Mayor de dos numeros"
							Escribir "ingrese la cantidad de numeros "
							Leer cantidad
							mayor_menor(i, cantidad,may,men,num)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"5":
							Escribir "Mayor de tres numeros"
							Escribir "ingrese la cantidad de numeros "
							Leer cantidad
							mayor_menor(i, cantidad,may,men,num)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"6":
							Escribir "Comprar productos"
							Escribir "*** Tienda, El Baraton***"
							Escribir "precio del producto"
							Leer preciocompra 
							compra(preciocompra, descuento, totaldescuento,IVA)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 5 Segundos
						"7":
							Escribir "Pago de Sueldos"
							Escribir "Digite sus nombres completos: "
							leer nombre
							escribir "Ingrese sus horas trabajadas: " 
							leer horastrabajadas
							Escribir "¿Cuánto es el valor por cada hora? "
							leer valorhora
							Escribir "Escriba sus horas50: "
							leer horas50
							Escribir "Escriba sus horas100: " 
							leer horas100
							PagoSueldo(sueldo,horastrabajadas,valorhora,horas50,horas100,descuento)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"8":
							Escribir "Notas de Alumnos"
							Escribir "deme la primera nota"
							leer nota1
							Escribir "deme la segunda nota"
							leer nota2
							notaalumnos(nota1,nota2,total)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"9":
							Escribir "Positivo/Negativo"
							Escribir "digite un numero"
							Leer num 
							positivonegativo(num)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"10":
							Escribir "Par e Impar"
							Escribir "dame un numero"
							Leer num1
							parimpar(num1)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"11":
							Escribir "Multiplo de cualquier Numero"
							Escribir "ingrese numero"
							leer num1
							Escribir "ingrese numero"
							leer num2
							multiplos(num1,num2)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"12":
							Escribir "El Mayor y Menor de una secuencia de numeros"
							Escribir "ingrese la cantidad de numeros "
							Leer cantidad
							mayor_menor(i, cantidad,may,men,num)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"13":
							Escribir "Positivos de una secuencia de numeros"
							Escribir "ingrese la cantidad de numeros"
							leer cantidad
							positivos(cantidad,num,i,positiv)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"14":
							Escribir "Suma de los multiplos de cinco de una serie de numeros"
							Escribir "se va a realizar una secuencia de numero que son multiplos de 5 y se van a sumar cuando el usuario precione -1"
							secuencia_sumaymultiplo_5(num1,sum,pro)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"15":
							Escribir "Generar y presentar los Numeros pares del 2 a N"
							Escribir "ingrese un numero"
							leer num1
							pares2_n(num1,i)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"16":
							Escribir "Cantidad, Suma y Promedio de numeros negativos de una serie"
							Escribir "cuantos numeros desea ingresar? "
							leer num1
							sumaypromedio_de_negativos(num1,num2,sum,prom)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"17":
							Escribir "Serie promedio de rango de numeros"
							Escribir 'ingrese cantidad de digitos del numero'
							Leer num
							promedio_de_100_500(a,num,num1,suma)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"18":
							Escribir "Validar que un numero sea positivo"
							Escribir " mientras el numero sea negativo se reiniciara y tendra que colocar un numero positivo"
							validar_positivo(n1)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"19":
							Escribir "Base y exponente"
							Escribir 'ingrese los numeros'
							Leer num1
							Leer num2
							baseelevada(num1,num2)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"20":
							Escribir "Serie Factoriales"
							Escribir "presenta una serie de numeros y se mostrara su factoria,la secuencia terminara cuando se precione 0 "
							factorial_hasta_0(num1,num2,fac)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"21":
							Escribir "Regresando Al Menu Principal..."
							Esperar 3 Segundos
							
						De Otro Modo:
							Escribir "Opcion Incorrecta"
							Esperar 3 Segundos
					Fin Segun
				Fin Mientras
			"2":
				opc2=""
				Mientras opc2<>"11" Hacer
					opc2= presentarMenu("********** M E N U  E J E R C I C I O S  D E  RAZONAMIENTO **********",menuRazonamiento,11)
					Borrar Pantalla
					Segun opc2 Hacer
						"1":
							Escribir "Cantidad de Digitos de un Numero"
							Escribir "ingrese un numero entero"
							leer num
							digitos_de_numero(dig,num)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"2":
							Escribir "Covertir de Base 2 a Base 10"
							Escribir "Ingresa un numero en binario"
							leer num
							binario_a_decimal(x,num,decimal,binario)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"3":
							Escribir "Convertir de Base 2 a Base 16 pasando por Base 10"
							decimal <- 0; solucion <- ''; base <- 0; b <- 'b'; oc <- 'o'; h <- 'h'
							Escribir "ingresa numero binario o decimal "
							Leer decimal
							Escribir "covertir a base ?"
							Escribir "binario (B)"
							Escribir "octal (O)"
							Escribir "hexadecimal(H)"
							Leer r
							Si r=b Entonces
								base <- 2
							SiNo
								Si r=oc Entonces
									base <- 8
								SiNo
									base <- 16
								FinSi
							FinSi
							Escribir conversordesdedecimal(decimal,base)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"4":
							Escribir "Serie de fibonacci hasta N"
							Escribir "Por favor ingrese numero"
							leer num
							fibonacci(num,num1,num2,num3)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"5":
							Escribir "Divisores de un Numero"
							Escribir "por favor ingrese el numero"
							leer num1 
							divisores_de_un_numero(num1)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
							
						"6":
							Escribir "Numero Perfecto"
							Escribir "si numero es 6 los divisores son 1,2,3 sumados igual a 6 es igual al numero ==> es perfecto"
							numero_perfecto(num,num1,perfecto)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
							
						"7":
							Escribir "Ejercicio Numero Primo"
							Escribir "Ingrese Numero: " Sin Saltar
							Leer num
							resp=primo(num)
							Si resp=1 Entonces
								Escribir num," Es Primo"
							SiNo
								Escribir num," No Es Primo"
							Fin Si
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"8":
							Escribir "Verificar si dos numeros son Amigos"
							Escribir "por favor ingrese el numero"
							leer num1,num2
							num_amigos(num1,num2,sumas)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"9":
							Escribir "Ejercicio Primos Gemelos"
							Escribir "Ingrese Numero 1: " Sin Saltar
							Leer num1
							Escribir "Ingrese Numero 2: " Sin Saltar
							Leer num2
							Si primo(num1)=1 y primo(num2)=1 y abs(num1-num2)=2 Entonces
								Escribir num1," y ",num2," Son Primos Gemelos"
							SiNo
								Escribir num1," y ",num2," No Son Primos Gemelos"
							Fin Si
							resp=primo(num)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"10":
							Escribir "De una serie de numeros cuantos son Primos"
							Escribir "dimencion del arreglo"
							Leer n
							si n <= 0 Entonces
								Escribir "error tiene que ser mayor a 0"
							SiNo
								Definir arreglo Como Entero
								rellenararreglo(arreglo,n)
								mostrarprimos(arreglo,n)
							FinSi
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"11":
							Escribir "Regresando Al Menu Principal..."
							Esperar 1 Segundos
							
						De Otro Modo:
							Escribir "Opcion Incorrecta"
							Esperar 3 Segundos
					Fin Segun
				Fin Mientras
			"3":
				opc3=""
				Mientras opc3<>"16" Hacer
					opc3= presentarMenu("********** M E N U  E J E R C I C I O S  C A D E N A S  Y  V E C T O R E S **********",menuCadenaVectores,16)
					Borrar Pantalla
					Segun opc3 Hacer
						"1":
							Escribir "llenar un arreglo de numeros"
							Escribir "Ingresar Valores a un Arreglo"
							Escribir "Ingrese cantidad de Elementos:" Sin Saltar
							Leer limite
							ingresarArreglo(arreglo,limite)
							Escribir "los valores se almacenaron en un arreglo"
							Esperar 3 Segundos
						"2":
							Escribir "Presentar los Valores de un Arreglo"
							arreglos(cantidad,i)
							Esperar 3 Segundos	
						"3":
							Escribir "Buscar un dato en un Arreglo"
							presentarArreglo(arreglo,1,limite)
							Escribir "Ingrese el dato a buscar en el arreglo"
							Leer dato
							pos = buscarArreglo(dato,arreglo,limite)
							Si pos > 0 Entonces
								Escribir dato," Se encuentra en la posicion: ",pos, " del arreglo"
							SiNo
								Escribir dato," No se encuentra en el arreglo"
							Fin Si
							Esperar 3 Segundos	
						"4":
							Escribir "Elemento Mayor de un arreglo"
							Escribir "arreglo de 10 digitos"
							mayor_de_un_arreglo(n,may,men)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"5":
							Escribir "Copiar los datos de un arreglo en otro invertido"
							Escribir 'Ingresa el tamaño del vector'
							leer n
							arregloinvertido(n)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"6":
							Escribir "Dados los arreglos arreglo1[] y arreglo2[] Sumarlos en al arreglo Suma[]"
							Escribir "arreglo de 10 digitos"
							suma_de_arreglos(i,suma,arreglo1,arreglo2 )
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"7":
							Escribir "Dado N numeros guardar en un arreglo los numeros primos"
							Escribir "a continuacion tendra que colocar numeros y el algoritmo le dara los que son numero primos"
							numeros_primos1(indice,indice1)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"8":
							Escribir "Dadas los datos de un arreglo obtener el promedio de las notas >=> 70"
							escribir "cuantos numeros desea almacenar en el arreglo"
							leer n 
							notas_promedios_arreglos( n, acum, prom)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
							
						"9":
							Escribir "Dada una cadena presentarla caracter por caracter"
							Escribir "Ingrese frase:" Sin Saltar
							Leer frase
							presentarCadena(frase)
							Esperar 3 Segundos
						"10":
							Escribir "Dada una cadena presentarla invertida"
							Escribir "dame una frase"
							Leer frase
							vocales(frase)
							Esperar 3 Segundos
						"11":
							Escribir "Dada una cadena indicar cuantos vocales tiene"
							Escribir "Ingrese frase:" Sin Saltar
							Leer frase
							vocales(frase)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"12":
							Escribir "Dada una cadena indicar cuantos palabras tiene"
							Escribir  Sin Saltar "Introduce una frase:" 
							Leer  cad 
							palabras(posicion ,cont,cad)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"13":
							Escribir "presentar la posicion de un caracter buscado dentro de una cadena"
							Escribir "HOLA "
							posicion_de_arreglo
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"14":
							Escribir "Dadas dos cadenas indicar si son iguales caracter por caracter"
							Escribir "debe ingresar un valor a la cadena 1 y a la cadena 2 "
							cadenas_igales_o_diferentes(x,c)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"15":
							Escribir "Indicar si una cadena es palindroma"
							Escribir " escriba una palabra"
							Leer palabra
							polindromo(a,b,c,x,palabra)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
						"16":
							Escribir "Regresando Al Menu Principal..."
							Esperar 1 Segundos
							
						De Otro Modo:
							Escribir "Opcion Incorrecta"
							Esperar 3 Segundos
					Fin Segun
				Fin Mientras
			"4":
				Escribir "Gracias por usar el sistema"	
			De Otro Modo:
				Escribir "Opcion incorrecta"
		Fin Segun
    Fin Mientras
FinAlgoritmo
